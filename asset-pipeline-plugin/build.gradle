buildscript {
    repositories {
        // mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.graceframework:grace-gradle-plugin:$graceVersion"
    }
}



group "org.graceframework.plugins"

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: "org.graceframework.grace-plugin"
apply plugin: "org.graceframework.grace-gsp"
apply plugin: "org.graceframework.grace-doc"
// apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    isReleaseVersion = !version.endsWith('SNAPSHOT')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    // mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter-logging'
    compileOnly "org.springframework.boot:spring-boot-starter-actuator"
    compileOnly "org.springframework.boot:spring-boot-autoconfigure"
    compileOnly "org.springframework.boot:spring-boot-starter-tomcat"
    api "org.springframework.boot:spring-boot-autoconfigure"
    annotationProcessor "org.springframework.boot:spring-boot-autoconfigure-processor"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    compileOnly "org.graceframework:grace-boot"
    implementation "org.graceframework:grace-web-common"
    implementation "org.graceframework:grace-web-taglib"
    implementation "org.graceframework:grace-web-url-mappings"
    implementation "commons-lang:commons-lang:2.6"
    implementation "com.github.ben-manes.caffeine:caffeine:3.1.8"
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    implementation project(':asset-pipeline-core'), {
        exclude group:'org.mozilla', module:'rhino'
        exclude group:'com.google.javascript', module:'closure-compiler-unshaded'
    }
    testImplementation("org.graceframework:grace-test-support")
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
}

configure([compileGroovy, compileTestGroovy]) {
    groovyOptions.fork(memoryInitialSize: '128M', memoryMaximumSize: '1G')
    groovyOptions.encoding = "UTF-8"
    options.encoding = "UTF-8"
    options.compilerArgs << '-parameters'
}

jar {
    enabled = true
    archiveClassifier.set('')
    includeEmptyDirs = false
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'asset-pipeline-plugin'
            artifact source: "${project.sourceSets.main.groovy.classesDirectory.get()}/META-INF/grails-plugin.xml",
                classifier: "plugin",
                extension: 'xml'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'Asset Pipeline Plugin'
                    description 'The Grace Asset-Pipeling plugin providing asset transpiling to grace.'
                    url 'https://github.com/grace-plugins/grace-asset-pipeline'
                    scm {
                        url 'https://github.com/grace-plugins/grace-asset-pipeline'
                        connection 'scm:https://github.com/grace-plugins/grace-asset-pipeline.git'
                        developerConnection 'scm:git@github.com/grace-plugins/grace-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
						developer {
							id 'rainboyan'
							name 'Michael Yan'
							email 'rain@rainboyan.com'
						}
                    }
                }
            }
            from components.java
        }
    }
}

afterEvaluate {
	signing {
		required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
		sign publishing.publications.maven
	}
}