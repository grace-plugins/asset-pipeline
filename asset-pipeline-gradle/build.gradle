buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
    }
    dependencies {
        classpath("com.gradle.plugin-publish:com.gradle.plugin-publish.gradle.plugin:1.3.0")
    }
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'idea'

group = 'org.graceframework.plugins'

ext.isReleaseVersion = !version.endsWith('SNAPSHOT')
ext.pomInfo = {
    delegate.url 'https://github.com/grace-plugins/grace-asset-pipeline'

    delegate.licenses {
        delegate.license {
            delegate.name 'The Apache Software License, Version 2.0'
            delegate.url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            delegate.distribution 'repo'
        }
    }

    delegate.scm {
        delegate.url 'scm:git@github.com:grace-plugins/grace-asset-pipeline.git'
        delegate.connection 'scm:git@github.com:grace-plugins/grace-asset-pipeline.git'
        delegate.developerConnection 'scm:git@github.com:grace-plugins/grace-asset-pipeline.git'
    }

    delegate.developers {
        delegate.developer {
            delegate.id 'rainboyan'
            delegate.name 'Michael Yan'
            delegate.email 'rain@rainboyan.com'
        }
    }
}


repositories {
    // mavenLocal()
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    api gradleApi()
    api localGroovy()

    api project(':asset-pipeline-core')
    api 'com.google.javascript:closure-compiler-unshaded:v20240317'
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0')
}

task(console, dependsOn: 'classes', type: JavaExec) {
main = 'groovy.ui.Console'
classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

gradlePlugin {
    website = 'https://github.com/grace-plugins/grace-asset-pipeline/'
    vcsUrl = 'https://github.com/grace-plugins/grace-asset-pipeline/.git'

    plugins {
        assetPipeline {
            id = 'org.graceframework.asset-pipeline'
            displayName = 'Grace Asset Pipeline Gradle Plugin'
            description = 'Asset-Pipeline is a plugin used for managing and processing static assets in Grace applications primarily via Gradle.'
            tags.set(['grace-framework', 'assets', 'asset-pipeline'])
            implementationClass = 'asset.pipeline.gradle.AssetPipelinePlugin'
        }
    }
}

afterEvaluate {
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign(publishing.publications.pluginMaven)
    }
}

project.afterEvaluate {
    project.publishing.publications.each { MavenPublication publication->
        publication.pom {
            if (publication.name == "maven" || publication.name == "pluginMaven") {
                name = "Grace Asset Pipeline Gradle Plugin"
                description = "Asset-Pipeline is a plugin used for managing and processing static assets in Grace applications primarily via Gradle."
            }

            if (publication.name == "pluginMaven" || publication.name.endsWith("PluginMarkerMaven")) {
                withXml {
                    def xml = asNode()
                    xml.children().last() + pomInfo
                }
            }
        }
    }
}