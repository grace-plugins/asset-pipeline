buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}



apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'signing'

group = 'org.graceframework.plugins'

ext.isReleaseVersion = !version.endsWith('SNAPSHOT')


repositories {
    mavenCentral()
}

configurations {
    provided
    doc.extendsFrom(runtime)
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        groovy.srcDirs   += ['src/main/java']
        java.srcDirs      = []
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

groovydoc {
    docTitle "Asset Pipeline Core"
    windowTitle "Asset Pipeline For the JVM API Doc"
    groovyClasspath = configurations.doc
    classpath = configurations.runtime
    // Links to external Javadoc, currently only the J2SE links are working properly 
      link('http://download.oracle.com/javase/8/docs/api/', 'java.', 'org.xml', 'javax.', 'org.xml.') 
      link('http://groovy.codehaus.org/api/',  'groovy.', 'org.codehaus.groovy.') 
}

dependencies {
    compileOnly    "org.codehaus.groovy:groovy:$groovyVersion"
    compileOnly    "org.codehaus.groovy:groovy-json:$groovyVersion"
    compileOnly    "org.codehaus.groovy:groovy-templates:$groovyVersion"
    doc         "org.codehaus.groovy:groovy-all:$groovyVersion"
    doc         'org.fusesource.jansi:jansi:1.18'
    api     'org.mozilla:rhino:1.7R4'
    api("org.graalvm.sdk:graal-sdk:22.0.0.2")
    compileOnly("org.graalvm.js:js:22.0.0.2")
    compileOnly("org.graalvm.js:js-scriptengine:22.0.0.2")

    compileOnly     'com.google.javascript:closure-compiler-unshaded:v20240317'
    api 'org.slf4j:slf4j-api:1.7.28'
    testImplementation project(':asset-pipeline-classpath-test')
    testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.28'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'asset-pipeline-core'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'Asset Pipeline Core'
                    description 'JVM Asset Pipeline library for serving static web assets, bundling, minifying, and extensibility for transpiling.'
                    url 'https://github.com/grace-plugins/grace-asset-pipeline'
                    scm {
                        url 'https://github.com/grace-plugins/grace-asset-pipeline'
                        connection 'scm:https://github.com/grace-plugins/grace-asset-pipeline.git'
                        developerConnection 'scm:git@github.com/grace-plugins/grace-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'rainboyan'
                            name 'Michael Yan'
                            email 'rain@rainboyan.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
}


task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}


test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


afterEvaluate {
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.maven
    }
}